/*
    https://www.json.org/json-en.html
    
    entrypoint is "json::VALUE"
*/

namespace json

<VALUE> => <WS> (<STRING> || <NUMBER> || <OBJECT> || <ARRAY> || "true" || "false" || "null") <WS>

<OBJECT> => (
    "{" <WS> "}" ||
    "{" <OBJECT-SEQUENCE>
)
<OBJECT-SEQUENCE> => <WS> <STRING> <WS> ":" <VALUE> <OBJECT-END>
<OBJECT-END> => "}" || "," <OBJECT-SEQUENCE>

<ARRAY> => (
    "[" <WS> "]" ||
    "[" <ARRAY-SEQUENCE>
)
<ARRAY-SEQUENCE> => <VALUE> <ARRAY-END>
<ARRAY-END> => "]" || "," <ARRAY-SEQUENCE>

<STRING> => "\"" <STRING-CONTENT>
<STRING-CONTENT> => (
    "\"" ||
    <UTF-8-CHAR> <STRING-CONTENT> ||
    <STRING-ESCAPE> <STRING-CONTENT>
)
<STRING-ESCAPE> => "\\" (
    "\"" ||
    "\\" ||
    "/" ||
    "b" ||
    "f" ||
    "n" ||
    "r" ||
    "t" ||
    "u" <HEX-DIGIT> <HEX-DIGIT> <HEX-DIGIT> <HEX-DIGIT>
)
<HEX-DIGIT> => u8 {
    0x30..0x39, 0x41..0x46, 0x61..0x66
}
<UTF-8-CHAR> => <UTF-8-CHAR-1> 
/*|| <UTF-8-CHAR-2> || <UTF-8-CHAR-3> || <UTF-8-CHAR-4>*/
<UTF-8-CHAR-1> => u8 { 32..33, 35..91, 93..127 }
<UTF-8-CHAR-2> => u8 { 192..223 } u8 { 128..191 }
<UTF-8-CHAR-3> => u8 { 224..239 } u8 { 128..191 } u8 { 128..191 }
<UTF-8-CHAR-4> => u8 { 240..247 } u8 { 128..191 } u8 { 128..191 } u8 { 128..191 }

/* Disabling whitespaces...
<WS> => (
    "" ||
    " " <WS> ||
    "\n" <WS> ||
    "\r" <WS> ||
    "\t" <WS>
)
*/
<WS> => ""

<NUMBER> => <SIGN> <LEADING-DIGITS> <FRACTION> <EXPONENT>
<SIGN> => "" || "-"
<LEADING-DIGITS> => "0" || <NON-ZERO-DIGIT> <MORE-DIGITS>
<NON-ZERO-DIGIT> => u8 { 0x31..0x39 }
<DIGIT> => u8 { 0x30..0x39 }
<DIGITS> => <DIGIT> <MORE-DIGITS>
<MORE-DIGITS> => "" || <DIGITS>
<FRACTION> => "" || "." <DIGITS>
<EXPONENT> => "" || ("e" || "E") ("" || "+" || "-") <DIGITS>
